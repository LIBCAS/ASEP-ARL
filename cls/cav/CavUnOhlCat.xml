<?xml version="1.0" encoding="UTF-8"?>
<Export generator="IRIS" version="26" zv="IRIS for UNIX (Red Hat Enterprise Linux 8 for x86-64) 2023.1.3 (Build 517U)" ts="2024-07-12 15:10:20">
<Class name="User.CavUnOhlCat">
<Description><![CDATA[
<pre>
12.07.24 ln; operace ipref
24.02.20 tt; zalozena metoda - writeCatRec
09.11.19 tt; optimalizace vymazu prednastavenych hodnot
18.09.17 tt; allowSaveEx: provedena uprava, aby se spravne ukladali ohlasy i pri prebrani zaznamu
20.11.15 tt; pridano generovani Uxx poli v allowSave
19.11.15 tt; allowSaveEx: pridana metoda 
30.04.14 tt; postSaveEx: Zalozena metoda post-save - zalozeno, aby se zmena bibliograficke citace
             zazmamu ohlasu promitla do vsech katalogovych zaznamu
09.04.14 tt; zalozena trida, kvuli zvlastnimu ukladani citaci a recenzi
             mozne dalsi rozsireni funkcnosti - zatim ale neni potreba
</pre>]]></Description>
<ClassType>persistent</ClassType>
<IncludeCode>Common,I2</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>User.DataTable</Super>
<TimeChanged>67033,54614.770073856</TimeChanged>
<TimeCreated>59592,28773.903995</TimeCreated>

<Parameter name="INDEXPARAMS">
<Description><![CDATA[
Toto su parametre pre indexovaciu rutinu.
viz. ..ixUpdate(), ixIndexValues(), ixIndexValuesEx()
a tiez ##class(SPIndex).ixIndexValues()<br><br>

Popis obsahu parametrov viz. desciption u SpIndex.ixIndexValues()<br><br>


POZOR: i ked je tento param pomenovany INDEXPARAMS
sluzi aj pre getDATA,allowSave,allowDelete a mozno neskor
aj dalsie callbacky. 
]]></Description>
<Default>C</Default>
</Parameter>

<Method name="classNameX">
<Description>
return current class unlike %ClassName this is a class method</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Library.String</ReturnType>
<Implementation><![CDATA[ quit "CavUnOhlCat"
]]></Implementation>
</Method>

<Method name="getDATAEx">
<Description><![CDATA[
<pre> interne Txx pro zaznamy

29.04.14 tt; zalozeno
--
</pre>
]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>handle:%Library.Binary</FormalSpec>
<Implementation><![CDATA[
  s shortFormat=##class(User.CavUnEpca).getDisplayFormatShort(.handle)
  
  s BibCit=##class(User.CavUnEpca).getBibcitCAV(.handle)
  
  s shortFormatOLD=##class(User.CavUnEpca).getDisplayFormatShortOLD(.handle)
  d:shortFormatOLD'="" $$$setTagX(.handle,"Tdi    "_$c(31)_"a"_shortFormatOLD)
  
  d:shortFormat'="" $$$setTagX(.handle,"Tzf    "_$c(31)_"a"_shortFormat)
  d:BibCit'="" $$$setTagX(.handle,"Tbc    "_$c(31)_"a"_BibCit)
]]></Implementation>
</Method>

<Method name="postSaveEx">
<Description>
post save handler - toto je metoda volana po uspesnom zapise zaznamu
(vcetne pripadneho updateindexov)
Navrat:
 "" nebo nejaka informace (text, return code), nerozlisuje se jestli jde o chybu nebo info,

30.04.14 tt; Zalozena metoda post-save - zalozeno, aby se zmena bibliograficke citace
             zazmamu ohlasu promitla do vsech katalogovych zaznamu</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&handle]]></FormalSpec>
<Implementation><![CDATA[
  s ret=""
  
  q ret
]]></Implementation>
</Method>

<Method name="allowSaveEx">
<Description><![CDATA[
<pre> Zalozena metoda pro potreby uprav zaznamu pri ukladani do ohlasu
Navrat:
 pri chybe "|ERR..." jinak prazdne nebo nejake info (INF, WARN, text)

18.09.17 tt; provedena uprava, aby se spravne ukladali ohlasy i pri prebrani zaznamu
20.11.15 tt; pridano generovani Uxx poli v allowSave
19.11.15 tt; pridana metoda 
</pre>]]></Description>
<ClassMethod>1</ClassMethod>
<CodeMode>code</CodeMode>
<FormalSpec><![CDATA[&handle:%Binary]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
  s ret=""
  ; radej zatim zakomentujeme, nema smysl, pozor pri odkomentovani poresit ERR navrat
  ; musime vygenerovat linky na 463 a 7xx - kvuli I3 formularum
  //s ret=##class(User.CavUnEpca).createAuthLinks7xx(.handle)     q:ret'="" ret
  //s ret=##class(User.CavUnEpca).createAuthLinks463(.handle)     q:ret'="" ret
  ; 20.11.15 tt; pridano generovani Uxx poli v allowSave    
  s sT969fo=""
  s sT969f=$$$getTagX(.handle,"969f")
  s sT001=$$$HandleT001(handle)       ; ziskani t001
  s sClass=$$$HandleClass(handle)     ; ziskani tridy
  s ipref=$$$Class2Ipref(sClass)      ; Ipref (instalacni prefix)
  
  if (sT001'="new")
  { ; odeslání záznamu - 969f - "D" - neodeslán/ "" nebo "A" odeslaný záznam zveřejněný v ipac2 
    if ##class(User.MARC).readX(.handleUS,sClass,sT001)
    { ; pokud se podari otevrit puvodni handle, muzeme zmenit stav
      s sT969fo=$$$getTagX(.handleUS,"969f")
      ; https://cosmo2/wiki/index.php/CAV:Formul%C3%A1%C5%99e_I3_dokumentace
      ; u CatUnOhlCat neni N, takze operace probehne vzdy
      d ##class(User.CavUnEpca).allowSaveExFormN(.handle,.handleUS, sT969f,sT969fo)
    }
  }
  else
  { ; 09.09.15 tt; pridany upravy pri odevzdani do ipacu z novych formularu
    ; https://cosmo2/wiki/index.php/CAV:Formul%C3%A1%C5%99e_I3_dokumentace
    d ##class(User.CavUnEpca).allowSaveExFormN(.handle,.sT969fo, sT969f,sT969fo)
  }  
  for i=1:1:4
  {
    s sPomDelT=""
    ; 09.11.19 tt; optimalizace vymazu prednastavenych hodnot
    s:(i=1) sPomDelT="014"
    s:(i=2) sPomDelT="U34"
    s:(i=3) sPomDelT="U14"
    s:(i=4) sPomDelT="U24"
    //s:(i=5) sPomDelT="017"
    continue:(sPomDelT="")
    s s014=$$$getTagXC(.handle,sPomDelT,-1)
    if (('$f(s014,($c(31)_"a")))&&(s014'=""))
    {
      d ##class(User.MARC).delTagX(.handle,sPomDelT)
    }
  }
 
  s sC99d=$$$getTagX(.handle,"C99d")
  
  s nC=##class(User.MARC).recordLineCountX(.handle) ; ziskani poctu radku v zaznamu 
  f i=1:1:nC 
  { ; pres vsechny radky zaznamu pujdeme po jednom radku a budeme nahrazovat
    s lsLine=##class(User.MARC).getLineX(.handle,i) continue:lsLine="" 
    s sTag=$e(lsLine,1,3), sOldLine=lsLine
    if (sTag="970")
    {
      s:(sC99d="DFLT_CZ_CIT_J") lsLine=$$$setSubTagStr(lsLine,$c(31)_"bJ")
      s:(sC99d="DFLT_CZ_CIT_B") lsLine=$$$setSubTagStr(lsLine,$c(31)_"bB")
      s:(sC99d="DFLT_CZ_CIT_M") lsLine=$$$setSubTagStr(lsLine,$c(31)_"bM")   
    }
      
   d:(sOldLine'=lsLine) ##class(User.MARC).setLineX(.handle,i,lsLine) ; zapsani to radku
  }
  d ##class(User.MARC).recordDelEmptyST(.handle)    ; odmazani prazdnych subtagu 
  
  ; 24.02.20 tt; provedena možná změna katalogových záznamů
  s ret=ret_..writeCatRec(.handle)
  
  q ret
]]></Implementation>
</Method>

<Method name="writeCatRec">
<Description><![CDATA[
<pre> Prenasaveno ukladani zmen do citaci do katalogovych zaznamu
24.02.20 tt; zalozena metoda - writeCatRec
</pre>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&handle:%Binary]]></FormalSpec>
<ProcedureBlock>1</ProcedureBlock>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
  s ret=""
  s BibCit=##class(User.CavUnEpca).getBibcitCAV(.handle)
  
  s ipref=$$$DbClassIpref      ; Ipref (instalacni prefix)
  s ictx=$zcvt(ipref,"L"), sT001=$$$HandleT001(handle)       ; ziskani t001

  s BibCit=$$$strswap(BibCit,"<em>","")   
  s BibCit=$$$strswap(BibCit,"</em>","")     
  s BibCit=$$$strswap(BibCit,"<strong>","")     
  s BibCit=$$$strswap(BibCit,"</strong>","") 
  s BibCit=$$$strswap(BibCit,"<u>","")      
  s BibCit=$$$strswap(BibCit,"</u>","") 
  s BibCit=$$$strswap(BibCit,".b.","")     
  s BibCit=$$$strswap(BibCit,"./b.","") 
  
  s id=""
  for  
  { ; cyklus pres vsechny navazane zaznamy
    s id=$o(^$$$MarcIndexG(ipref_"UnEpca","auk"," "_ictx_"_un_ohl_cat*"_sT001,id))
    q:(id="")   
    
    if ##class(User.MARC).getDATAX(.handleCAT,id)
    { ; otevren zaznam z katalogu, kde je navazany ohlas - projdem vsechny 
      ; 971 aC71 a zkusime aktualizaovat zaznamy
      s nC=##class(User.MARC).recordLineCountX(.handleCAT) ; ziskani poctu radku v zaznamu 
      f i=1:1:nC 
      { ; pres vsechny radky zaznamu pujdeme po jednom radku a budeme nahrazovat
        s lsLine=##class(User.MARC).getLineX(.handleCAT,i) 
        continue:(lsLine="")
        s sTag=$e(lsLine,1,3)
        if ((sTag="971")||(sTag="C71"))
        { ; pokud narazim na tag, ktery mam kontrolovat, zkontroluji a prepisu bibliografickou citaci, pokud se zmenila
          s sLx=$$$getSubTagStr(lsLine,"x"),sT001rec=ictx_"_un_ohl_cat*"_sT001
          if (sLx=sT001rec)
          { ; pokud mame v x link na tento zaznam z CavUnOhlCat
            s sLi=$$$getSubTagStr(lsLine,"i")

            if ($tr(sLi," ","") '=$tr(BibCit," ",""))
            { ; pokud se nam zmenila bibliograficka citace
              s lsLine=$$$setSubTagStr(lsLine,$c(31)_"i"_BibCit) ; ulozime si bibliografickou citaci do radku
              d ##class(User.MARC).setLineX(.handleCAT,i,lsLine) ; zapiseme si radek do hanldu
              ; handle ulozime
              s sOk=##class(User.MARC).writeX(.handleCAT)
              if '$$$ISOK(sOk) 
              { ; pokud mam chybu, vypisu 
                w !,"Chyba zapisu handlu"    
                zt "AC3" ; Nedoslo k ulozeni navazanych zaznamu zaznamu
                //s ret=ret_"|ERROHL001#Nedošlo k uložení navázaných katalogových záznamů (buď je někdo edituje, nebo obsahují závažnou chybu). Kód: "_$$$HandleT001(handleCAT)
              }              
            }
          }
        }       
      }
    }      
  }  
  q ret
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Storage.Persistent</Type>
</Storage>
</Class>
</Export>
